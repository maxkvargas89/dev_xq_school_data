config {
    type: "table",
    description: "Transformation of CA DOE file to dispaly graduation counts and rates at school level",
    tags: ["ca"]
}

js {
  const { applyCodeMapping } = require("definitions/sources/includes/utilities/ca_doe_reporting_category_mapping")
}

select 
    academic_year as year,
    concat(lpad(county_code,2),lpad(cast(district_code as STRING),5),lpad(school_code,7)) as cds_code,
    county_name,
    district_name,
    school_name,
    charter_school as charter,
    dass,
    ${applyCodeMapping('reporting_category')} as reporting_category,
    cast((case when regular_hs_diploma_graduates_count_ = '*' then '0' else regular_hs_diploma_graduates_count_ end) as INT64) as count_graduates,
    cast((case when dropout_count_ = '*' then '0' else dropout_count_ end) as INT64) as count_dropouts,
    cast((case when still_enrolled_count_ = '*' then '0' else still_enrolled_count_ end) as DECIMAL) as count_still_enrolled,
    cast((case when regular_hs_diploma_graduates_rate_ = '*' then '700.0' else regular_hs_diploma_graduates_rate_ end) as DECIMAL) as perc_graduate,
    cast((case when dropout_rate_ = '*' then '700.0' else dropout_rate_ end) as DECIMAL) as perc_dropout,
    cast((case when still_enrolled_rate_ = '*' then '700.0' else still_enrolled_rate_ end) as DECIMAL) as perc_still_enrolled
from ${ref("ca_doe_graduation_rate")}
where aggregate_level = 'S'


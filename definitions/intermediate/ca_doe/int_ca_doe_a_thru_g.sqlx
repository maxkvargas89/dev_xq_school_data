config {
    type: "table",
    description: "Transformation of CA DOE file to dispaly counts and rates of students graduating with A-G and/or Golden Seal, at school level",
    tags: ["ca"]
}

js {
  const { applyCodeMapping } = require("definitions/sources/includes/utilities/ca_doe_reporting_category_mapping")
}

select 
    academic_year as year,
    concat(lpad(county_code,2),lpad(cast(district_code as STRING),5),lpad(school_code,7)) as cds_code,
    county_name,
    district_name,
    school_name,
    charter_school as charter,
    dass,
    ${applyCodeMapping('reporting_category')} as reporting_category,
    cast((case when met_uc_csu_grad_req_s_count_ = '*' then '0' else met_uc_csu_grad_req_s_count_ end) as DECIMAL) as count_a_g_grads,
    cast((case when met_uc_csu_grad_req_s_rate_ = '*' then '700.0' else met_uc_csu_grad_req_s_rate_ end) as DECIMAL) as perc_a_g_grad,
    cast((case when golden_state_seal_merit_diploma_count_ = '*' then '0' else golden_state_seal_merit_diploma_count_ end) as DECIMAL) as count_golden_state_seal_grads,
    cast((case when golden_state_seal_merit_diploma_rate = '*' then '700.0' else golden_state_seal_merit_diploma_rate end) as DECIMAL) as perc_golden_state_seal_grad
from ${ref("ca_doe_a_thru_g")}
where aggregate_level = 'S'


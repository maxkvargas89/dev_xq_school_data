config {
    type: "table",
    description: "Tranformation of CA DOE file to display chronic absenteeism counts and rates at the school level",
    tags: ["ca"]
}

js {
  const { applyCodeMapping } = require("definitions/sources/includes/utilities/ca_doe_reporting_category_mapping")
}

select academic_year as year,
    concat(county_code,lpad(cast(district_code as STRING),5,'0'),school_code) as cds_code,
    county_name,
    district_name,
    school_name,
    coalesce(charter_yn,charter_school) as charter,
    dass,
    ${applyCodeMapping('reporting_category')} as reporting_category,
    cast((case when chronic_absenteeism_count = '*' then '0' else chronic_absenteeism_count end) as INT64) as count_chronic_absenteeism,
    cast((case when coalesce(chronic_absenteeism_eligible_cumula,chronic_absenteeism_eligible_cumulative_enrollment) = '*' then '0' else coalesce(chronic_absenteeism_eligible_cumula,chronic_absenteeism_eligible_cumulative_enrollment) end) as INT64) as count_total_enrollment,
    cast((case when chronic_absenteeism_rate = '*' then '700.0' else chronic_absenteeism_rate end) as DECIMAL) as perc_chronic_absenteeism
from ${ref("ca_doe_chronic_absenteeism")}
where aggregate_level = 'S'


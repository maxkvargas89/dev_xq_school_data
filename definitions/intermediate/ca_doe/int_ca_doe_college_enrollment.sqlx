config {
    type: "table",
    description: "Tranformation of CA DOE file to display college going types at the school level",
    tags: ["ca"]
}

js {
  const { applyCodeMapping } = require("definitions/sources/includes/utilities/ca_doe_reporting_category_mapping")
}

select academic_year as year,
    concat(county_code,lpad(cast(district_code as STRING),5,'0'),school_code) as cds_code,
    county_name,
    district_name,
    school_name,
    charter_school as charter,
    alternative_school_accountability_status as alternative,
    (case when completer_type = 'AGN' then 'Graduate not meeting A-G'
        when completer_type = 'AGY' then 'Graduate meeting A-G'
        when completer_type = 'NGC' then 'Non graduate not meeting A-G'
        when completer_type = 'TA' then 'All high school completers' end) as completer_type,
    ${applyCodeMapping('reporting_category')} as reporting_category,
    cast((case when high_school_completers = '*' then '0' else high_school_completers end) as INT64) as high_school_completers,
   cast((case when enrolled_in_college_total_12_months_ = '*' then '0' else enrolled_in_college_total_12_months_ end) as INT64) as count_enroll_college_12mos,
   cast((case when college_going_rate_total_12_months_ = '*' then '700.0' else college_going_rate_total_12_months_ end) as DECIMAL) as perc_enroll_college_12mos,
    cast((case when enrolled_uc_12_months_ = '*' then '0' else enrolled_uc_12_months_ end) as INT64) as count_enroll_uc_12mos,
    cast((case when enrolled_csu_12_months_ = '*' then '0' else enrolled_csu_12_months_ end) as INT64) as count_enroll_csu_12mos,
    cast((case when enrolled_ccc_12_months_ = '*' then '0' else enrolled_ccc_12_months_ end) as INT64) as count_enroll_ca_cc_12mos,
from ${ref("ca_doe_college_enrollment")}
where aggregate_level = 'S'

